#!/usr/bin/env ys-0

# This is program provides a way to use YS (YAMLScript) for Helm templating.
# See https://github.com/kubeys/helmys for full details.

HELMYS-VERSION =: '0.1.7'

helm-commands =: qw(
  create dependency env get help history install lint list package plugin pull
  push registry repo rollback search show status template test uninstall
  upgrade verify version)

# When helmys is called with no args it is assumed to be a post renderer and
# will read from stdin.
#
# When called with args, it is a CLI utility. Currently its only function is to
# initialize YS support in an existing Helm chart.
defn main:
  # No args. Do post-render:
  (): post-render()
  # Got args. Do init or proxy to helm:
  (cmd *args):
    condp eq cmd:
      '--version':
        say: "helmys v$HELMYS-VERSION"

      '-h': print-help()
      '--help': print-help()

      'init': do-init(args*)
      # TODO: Add 'update' support to update the helmys install to latest

      # Call helm command adding '--post-renderer=helmys'
      'install'  : exec-helm-with-post-renderer(cmd args*)
      'template' : exec-helm-with-post-renderer(cmd args*)
      'upgrade'  : exec-helm-with-post-renderer(cmd args*)

      # In cmd is a alpha word then assume it's a helm command.
      # This allows people to 'alias helm=helmys' in their shell.
      else:
        if cmd.in?(helm-commands):
          exec-helm-command: cmd args*
          die: "Invalid helmys subcommand: '$cmd'"

defn print-help():
  say: |

    helmys - YS (YAMLScript) tool for Helm chart templates

    The 'helmys' command is a Helm post-renderer that allows you to use YS code
    in your Helm charts. See https://yamlscript.org/helmys for more info.

    Usage: helm <cmd> <args> --post-renderer=helmys
       or: helmys [<cmd>] [<arg...>]

    When called with no arguments, helmys acts as a post-renderer converting
    YAML templates containing YS code from stdin, to YAML ready for Kubernetes.

    The helmys command can also be used as a handy short form for the helm
    commands that have a '--post-renderer' option.
    Just change 'helm' to 'helmys' in your command and you can skip typing
    the '--post-renderer=helmys' option at the end.

    Commands:

      install       Shortcut for 'helm install <args> --post-renderer=helmys'
      template      Shortcut for 'helm template ...'
      upgrade       Shortcut for 'helm upgrade ...'

      -h, --help    Print this help and exit
      -v, --version Print the helmys command version and exit


# Initialize a Helm chart by adding 2 helmys template files.
# Error if chart dir is not actually a Helm chart or is already initialized.
defn do-init:
  # 'helmys init' expects no extra args:
  (_ *xs):
    die: |
      Invalid usage.
      Expected 'helmys init <chart-dir>'

  (chart):
    # Check that dir is an uninitialized Helm chart:
    when-not fs-d("$chart/templates"):
      die: "'$chart' is not a chart directory"
    when fs-f("$chart/bin/helmys"):
      die: "'$chart' is not a chart directory"

    templates =: "$chart/templates"

    write "$templates/helmys.yaml": init-helmys-yaml()
    write "$templates/helpers.yaml": init-helpers-yaml()

# Make a regex to find a chart by name:
defn template-re(name):
  name .=: replace('.' '\.')
  qr: "^# Source:\ .*/templates/$name"

defn post-render():
  when+ ENV.HELMYS_HOOKS.?:
    load-hooks-library: _

  # Read rendered YAML from Helm:
  yaml-in =: read(IN)

  # Write input if HELMYS_DEBUG_INPUT set:
  when-let file ENV.HELMYS_DEBUG_INPUT:
    write file: yaml-in

  # Split YAML stream into docs:
  yaml-docs =:
    yaml-in
      .split(/(?m)^---\n/):rest
  # Find helmys.yaml doc:
  helmys-doc =:
    yaml-docs
      .filter(template-re('helmys.yaml')):first
  # Find helpers.yaml doc:
  helpers-doc =:
    yaml-docs
      .filter(template-re('helpers.yaml')):first

  # Find the helmys charts and put them at the start of the stream.
  # They define things needed by YS in Helm templates.
  yaml-thru =:
    # Remove helmys templates from the input stream:
    helm-docs =: yaml-docs
      .remove(template-re('helmys.yaml'))
      .remove(template-re('helpers.yaml'))

    # TODO - Warn if a template looks YS but is missing !YS-v0: tag

    # Re-add the helmys templates to the start of the stream:
    ([helmys-doc helpers-doc] + helm-docs):
      .remove(nil?).join("\n---\n")

  # Write thruput if HELMYS_DEBUG_THRUPUT set:
  when-let file ENV.HELMYS_DEBUG_THRUPUT:
    write file: yaml-thru

  # Use YS to evaluate the now adjusted input stream.
  data-out =:
    try:
      ys/eval-stream: yaml-thru
      catch e:
        die: "helmys failed to render YS helm templates:\n$e"

  # Apply any user specified hooks:
  hook =: value('helmys-hook-out')
  data-out =:
    mapv _ data-out:
      fn(node):
        if hook:
          or _ node:
            hook: node
          else: node

  # Generate the final YAML output by using YS to evaluate the now adjusted
  # input stream.
  yaml-out =:
    yaml/dump-all: data-out

  # Assert that the final output is not empty:
  when yaml-out.!:
    die: "helmys produced no output from YS helm templates"

  # Write output if HELMYS_DEBUG_OUTPUT set:
  when-let file ENV.HELMYS_DEBUG_OUTPUT:
    write file: yaml-out

  # Write the final YAML output to STDOUT:
  print: yaml-out

defn load-hooks-library(lib):
  ys/eval:
    if lib =~ /^https:/:
      curl: lib
      read: lib

# Execute helm command with same args, plus '--post-renderer=helmys':
defn exec-helm-with-post-renderer(cmd *args):
  when args.some(\(/^--post-renderer/)):
    die: "Don't use the '--post-renderer' option with 'helmys'"

  when fs-f('values.ys'):
    write 'values.yaml':
      sh('ys -Y values.ys').out

  command =: "helm $cmd $joins(args) --post-renderer=helmys"
  warn: "+ $command"
  exec: command

# Execute helm command with same args:
defn exec-helm-command(cmd *args):
  command =: "helm $cmd $joins(args)"
  warn: "+ $command"
  exec: command

#------------------------------------------------------------------------------

defn init-helmys-yaml():: |
  !YS-v0

  #------------------------------------------------------------------------------
  # Use Go templating to expand the Helm builtin objects into YS variables:
  #
  # * Release
  # * Values
  # * Chart
  # * Subcharts
  # * Capabilities
  # * Template
  # * Files
  #
  # See: https://helm.sh/docs/chart_template_guide/builtin_objects/
  #------------------------------------------------------------------------------

  {{ range $key, $value := . }}
  {{ $key }} =::
  {{- toYaml $value | nindent 2 }}
  {{ end }}

defn init-helpers-yaml():: |
  !YS-v0

  #------------------------------------------------------------------------------
  # Define YS variables and functions to use in templates.
  #------------------------------------------------------------------------------
  defn trunc(s): take(63 s).str(*).replace(/-$/)

  # Expand the name of the chart:
  chart-name =:
    trunc: Values.nameOverride ||| Chart.name

  # Create a default fully qualified app name.
  chart-fullname =:
    if Values.fullnameOverride.?:
      trunc: Values.fullnameOverride
      else:
        name =: Values.nameOverride ||| Chart.name
        if name.has?(Release.Name):
          trunc: Release.Name
          format "%s-%s": Release.Name name

  # Selector labels:
  selectorLabels =::
    app.kubernetes.io/name:: Chart.name
    app.kubernetes.io/instance:: Release.Name

  # Chart labels:
  chart-labels =::
    helm.sh/chart:: "$(Chart.name)-$(Chart.version)"
    :: selectorLabels
    app.kubernetes.io/version:: Chart.appVersion
    app.kubernetes.io/managed-by:: Release.Service

  # Create the name of the service account to use:
  serviceAccountName =:
    Values.serviceAccount.name |||:
      if Values.serviceAccount.create:
        chart-fullname
        'default'


#------------------------------------------------------------------------------
create-ns: gh:q

intern main:q gh-url:q:
  fn(spec):
    owner repo ref *path =: spec.split('/')
    format 'https://raw.githubusercontent.com/%s/%s/refs/heads/%s/%s':
      owner repo ref path.join('/')

intern main:q load:q:
  fn(url):
    if url =~ /^https:/:
      ys/eval: curl(url)
      ys/load-file: url
