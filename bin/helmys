#!/usr/bin/env ys-0

# This is program provides a way to use YS (YAMLScript) for Helm templating.
# See https://github.com/kubeys/helmys for full details.

HELMYS-VERSION =: '0.1.4'

# When helmys is called with no args it is assumed to be a post renderer and
# will read from stdin.
#
# When called with args, it is a CLI utility. Currently its only function is to
# initialize YS support in an existing Helm chart.
defn main:
  # No args. Do post-render:
  (): post-render()
  # Got args. Do init or proxy to helm:
  (cmd *args):
    condp eq cmd:
      'init':
        do-init: args*

      'install'  : helm-with-post-renderer(cmd args*)
      'template' : helm-with-post-renderer(cmd args*)
      'upgrade'  : helm-with-post-renderer(cmd args*)

      # TODO: Add 'update' support.
      '--version':
        say: "helmys v$HELMYS-VERSION"

      else:
        die: "Invalid command: 'helmys $str(args)'"

# Initialize a Helm chart by adding 2 helmys template files.
# Error if chart dir is not actually a Helm chart or is already initialized.
defn do-init:
  # 'helmys init' expects no extra args:
  (_ *xs):
    die: |
      Invalid usage.
      Expected 'helmys init <chart-dir>'

  (chart):
    # Check that dir is an uninitialized Helm chart:
    when-not fs-d("$chart/templates"):
      die: "'$chart' is not a chart directory"
    when fs-f("$chart/bin/helmys"):
      die: "'$chart' is not a chart directory"

    templates =: "$chart/templates"
    when fs-f("$templates/helmys.yaml"):
      die: "'$chart' already initialized"

    # Ininitialize the Helm chart:
    if ENV.HELMYS_TEST:
      # Init with local files for `make test`:
      shell: "cp templates/helmys.yaml
                 templates/helpers.yaml
                 $templates"
      else:
        # Init with files from source repository:
        raw =: "https://raw.githubusercontent.com/\
                kubeys/helmys/refs/heads/main"
        shell: "curl -s -o $templates/helmys.yaml
                  $raw/templates/helmys.yaml"
        shell: "curl -s -o $templates/helpers.yaml
                  $raw/templates/helpers.yaml"

# Make a regex to find a chart by name:
defn template-re(name):
  name .=: replace('.' '\.')
  qr: "^# Source:\ .*/templates/$name"

defn post-render():
  # Read rendered YAML from Helm:
  yaml-in =: slurp(IN)

  # Write input if HELMYS_DEBUG_INPUT set:
  when+ ENV.HELMYS_DEBUG_INPUT: _.spit(yaml-in)

  # Split YAML stream into docs:
  yaml-docs =:
    yaml-in
      .split(/(?m)^---\n/):rest
  # Find helmys.yaml doc:
  helmys-doc =:
    yaml-docs
      .filter(template-re('helmys.yaml')):first
  # Find helpers.yaml doc:
  helpers-doc =:
    yaml-docs
      .filter(template-re('helpers.yaml')):first

  # Find the helmys charts and put them at the start of the stream.
  # They define things needed by YS in Helm templates.
  yaml-thru =:
    if not(helmys-doc) || not(helpers-doc):
      # Setting the HELMYS_PASSTHRU environment variable allows helmys to be
      # used as a post-renderer even when the chart doesn't use HelmYS at all.
      # This is to support the helm command being wrapped in a way that always
      # uses `--post-renderer=helmys` so that you can use `helm install` on any
      # chart without needing to knw in advance if it uses HelmYS for
      # templating.
      if not(ENV.HELMYS_PASSTHRU):
        die: |
          HelmYS templates missing for `--post-renderer=helmys`.
          Set `export HELMYS_PASSTHRU=1` to allow any chart to install.
        else:
          yaml-in

      else:
        # Remove helmys templates from the input stream:
        helm-docs =: yaml-docs.remove(template-re('helmys.yaml'))
        helm-docs =: helm-docs.remove(template-re('helpers.yaml'))

        # TODO - Warn if a document looks like a YS template but is missing
        # !yamlscript/v0:

        # Re-add the helmys templates to the start of the stream:
        join "\n---\n":
          helm-docs
            .cons(helpers-doc)
            .cons(helmys-doc)

  # Write thruput if HELMYS_DEBUG_THRUPUT set:
  when+ ENV.HELMYS_DEBUG_THRUPUT:
    _.spit(yaml-thru)

  # Generate the final YAML output by using YS to evaluate the now
  # adjusted input stream.
  yaml-out =:
    try:
      do:
        # XXX shell out to ys until std/eval-stream is available
        res =:
          sh {:in yaml-thru}: "ys -Ys -"
        when+ res.err.?:
          die: -"helmys failed to render YAMLScript helm templates:\n" + _
        get res: :out
      catch e:
        die: "helmys failed to render YS helm templates:\n$e"

  # Assert that the final output is not empty:
  when yaml-out.!:
    die: "helmys produced no output from YS helm templates"

  # Write output if HELMYS_DEBUG_OUTPUT set:
  when+ ENV.HELMYS_DEBUG_OUTPUT:
    _.spit(yaml-out)

  # Write the final YAML output to STDOUT:
  print: yaml-out

defn helm-with-post-renderer(cmd *args):
  when args.some(\(/^--post-renderer/)):
    die: "Don't use the '--post-renderer' option with 'helmys'"

  command =: "helm $cmd $joins(args) --post-renderer helmys"
  warn: "+ $command"
  exec: command
