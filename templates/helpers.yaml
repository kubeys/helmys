!yamlscript/v0
#------------------------------------------------------------------------------
# Define YAMLScript variables and functions to use in templates
#------------------------------------------------------------------------------
defn trunc(s): take(63 s).str(*).replace(/-$/)

# Expand the name of the chart:
chart-name =: trunc(Values.nameOverride ||| Chart.name)

# Create a default fully qualified app name.
chart-fullname =:
  if Values.fullnameOverride.?:
    then: Values.fullnameOverride:trunc
    else:
      name =: Values.nameOverride ||| Chart.name
      if name.has?(Release.Name):
        trunc: Release.Name
        format "%s-%s": Release.Name name

# Selector labels:
selectorLabels =::
  app.kubernetes.io/name:: Chart.name
  app.kubernetes.io/instance:: Release.Name

# Chart labels:
chart-labels =:
  apply merge::
  - helm.sh/chart:: "$(Chart.name)-$(Chart.version)"
  - ! selectorLabels
  - app.kubernetes.io/version:: Chart.appVersion
    app.kubernetes.io/managed-by:: Release.Service

# Create the name of the service account to use:
serviceAccountName =:
  or? Values.serviceAccount.name:
    if Values.serviceAccount.create:
      chart-fullname
      'default'
